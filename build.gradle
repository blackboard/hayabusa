apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "versions"

// define the version for the project when publishing to maven
group "blackboard.plugin"
version "1.0-SNAPSHOT"

buildscript {
  repositories { mavenCentral() }

  dependencies { // Add the versions plugin - use dependencyUpdates to detect updates to project dependencies
    classpath "com.github.ben-manes:gradle-versions-plugin:0.3" }
}

task wrapper(type: Wrapper) { gradleVersion = "1.6" }

project.ext {
  learnVersion = "9.1.100401.0"
  springVersion = "3.2.2.RELEASE"

  libraries = [
    spring: [
      "org.springframework:spring-beans:$springVersion",
      "org.springframework:spring-context:$springVersion",
      "org.springframework:spring-core:$springVersion",
      "org.springframework:spring-expression:$springVersion",
      "org.springframework:spring-web:$springVersion",
      "org.springframework:spring-webmvc:$springVersion",
      "org.springframework:spring-context-support:$springVersion",
      "org.codehaus.jackson:jackson-jaxrs:1.9.2"
    ],
    logging: [
      "ch.qos.logback:logback-classic:1.0.13",
      "org.slf4j:log4j-over-slf4j:1.7.5"
    ]
  ]
}

repositories {
  mavenCentral()
  maven { url "https://maven.blackboard.com/content/repositories/releases/" }
}

configurations { buildUtils }

dependencies {
  // providedCompile dependencies are libraries needed to build, but should not be included in the B2 WAR
  providedCompile( "blackboard.platform:bb-platform:$learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-cms-admin:$learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-taglibs:$learnVersion" ) { transitive = false }

  compile libraries.spring,
          libraries.logging,
          "com.google.guava:guava:14.0.1"

  compile( "blackboard.platform:bb-spring-webapi:10.1" ) { transitive = false }

  testCompile "junit:junit:4.11",
              "org.mockito:mockito-core:1.9.5",
              "org.springframework:spring-test:$springVersion"

  buildUtils "org.oscelot:b2deploy-task:0.1.0"
}


// Add a task to deploy a B2 using starting block
task deployB2( dependsOn: "war" ) << {
  ant.taskdef( name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath )
  ant.b2deploy( localfilepath: project.war.archivePath, host: "devday.blackboard.com", courseorgavailable: 'true', clean: 'true' )
}
